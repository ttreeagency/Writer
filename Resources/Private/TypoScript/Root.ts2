prototype(Ttree.Writer:RawContent) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Ttree.Writer/Private/Templates/TypoScriptObjects/RawContent.html'
	@override.singleCollection = ${Configuration.setting('Ttree.Writer.singleCollection')}
	columns = Neos.Fusion:Collection {
		collection = ${singleCollection ? q(node).children('[instanceof TYPO3.Neos:ContentCollection][_name = ' + singleCollection + ']') : q(node).children('[instanceof TYPO3.Neos:ContentCollection]')}
		itemName = 'node'
		itemRenderer = Ttree.Writer:ContentCollection
	}
}

prototype(Ttree.Writer:ContentCollection) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Ttree.Writer/Private/Templates/TypoScriptObjects/ContentCollectionTemplate.html'

	node = ${node}

	title = ${documentNode.label}

	showName = ${Configuration.setting('Ttree.Writer.singleCollection') ? FALSE : TRUE}

	attributes = Neos.Fusion:Attributes {
		class = 'column'
	}

	columnContent = TYPO3.Neos:ContentCollection {
		nodePath = '.'
	}
}

rawContent >
rawContent = TYPO3.Neos:Page {
	head {
		writerBackendHeader = Neos.Fusion:Template {
			@position = 'after neosBackendHeader'
			templatePath = 'resource://Ttree.Writer/Private/Templates/TypoScriptObjects/WriterBackendHeaderData.html'
			node = ${node}

			@cache {
				mode = 'uncached'
				context {
					1 = 'node'
				}
			}
			@if.onlyRenderWhenNotInLiveWorkspace = ${node.context.workspace.name != 'live'}
		}
	}



	bodyTag.attributes.class = 'writer-mode'

	body {
		templatePath = 'resource://Ttree.Writer/Private/Templates/Page/Default.html'
		sectionName = 'body'

		allContentCollections = TYPO3.Neos:PrimaryContent {
			nodePath = '.'
			default.renderer = Ttree.Writer:RawContent
		}
	}
}
