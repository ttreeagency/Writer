prototype(Ttree.Writer:RawContent) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Ttree.Writer/Private/Templates/RawContent.html'
	columns = Neos.Fusion:Collection {
		collection = ${Configuration.setting('Ttree.Writer.singleCollection') ? q(node).children('[instanceof Neos.Neos:ContentCollection][_name = ' + singleCollection + ']') : q(node).children('[instanceof Neos.Neos:ContentCollection]')}
		itemName = 'node'
		itemRenderer = Ttree.Writer:ContentCollection
	}
}

prototype(Ttree.Writer:ContentCollection) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Ttree.Writer/Private/Templates/ContentCollectionTemplate.html'

	node = ${node}

	title = ${documentNode.label}

	showName = ${Configuration.setting('Ttree.Writer.singleCollection') ? false : true}

	attributes = Neos.Fusion:Attributes {
		class = 'column'
	}

	columnContent = Neos.Neos:ContentCollection {
		nodePath = '.'
	}
}

rawContent >
rawContent = Neos.Neos:Page {
	head {
		writerBackendHeader = Neos.Fusion:Template {
			@position = 'after neosBackendHeader'
			templatePath = 'resource://Ttree.Writer/Private/Templates/WriterBackendHeaderData.html'
			node = ${node}

			@cache {
				mode = 'uncached'
				context {
					1 = 'node'
				}
			}

			@if.onlyRenderWhenNotInLiveWorkspace = ${node.context.workspace.name != 'live'}
		}
	}



	bodyTag.attributes.class = 'writer-mode'

	body {
		templatePath = 'resource://Ttree.Writer/Private/Templates/Page/Default.html'
		sectionName = 'body'

		allContentCollections = Neos.Neos:PrimaryContent {
			nodePath = '.'
			default.renderer = Ttree.Writer:RawContent
		}
	}
}
