prototype(Ttree.Writer:ContentRenderer.CustomLayout) < prototype(Neos.Fusion:Component) {
	content = Neos.Fusion:Collection {
		collection = ${WriterLayout.getLayout(documentNode)}
		itemKey = 'blockName'
		itemName = 'configuration'
		itemRenderer = Neos.Fusion:Case {
			property {
				condition = ${WriterLayout.getBlockType(documentNode, blockName) == 'property'}
				renderer = Ttree.Writer:Renderer.Property
			}
			childNode {
				condition = ${WriterLayout.getBlockType(documentNode, blockName) == 'childNode'}
				renderer = Ttree.Writer:Renderer.ChildNode
			}
			default {
				condition = ${true}
				renderer = Ttree.Writer:Renderer.Fusion
			}
		}

		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}

	renderer = afx`
	<div id="writer-container">
		<div class="writer-content-contentcollections">
			<div class="writer-content-contentcollection-container">
				{props.content}
			</div>
		</div>
	</div>
	`
}
