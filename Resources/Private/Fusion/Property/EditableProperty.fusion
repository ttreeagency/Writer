prototype(Ttree.Writer:Property.Editable) < prototype(Neos.Fusion:Component) {
    property = null
    propertyName = null
    content = null
    helpContent = null
    propertyValue = null
    editableNode = ${node}
    children = Neos.Fusion:RawArray
    badges = Neos.Fusion:RawArray

    renderer = Ttree.Writer:ContentWrapper {
        propertyName = ${props.propertyName}

        editButton = Neos.Neos:ContentElementEditable {
            node = ${props.editableNode}
            property = ${props.property}

            @if.hasStructuredEditing = ${props.property != null && NodeTypeConfiguration.hasStructuredEditing(props.editableNode.nodeType, props.property)}
        }

        content = Neos.Fusion:Case {
            rawContent {
                condition = ${props.content != null}
                renderer = ${props.content}
            }

            propertyValue {
                condition = ${props.propertyValue != null}
                renderer = ${props.propertyValue}
            }

            default {
                condition = true
                renderer = ${q(props.editableNode).property(props.property)}
            }
        }

        helpContent = ${props.helpContent}

        children = ${props.children}
        badges = ${props.badges}
    }
}
